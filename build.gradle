plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.7'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'

    /* API Generation */
    id "org.openapi.generator" version "6.4.0" // 6.3.0 ; previously used version a bit shitty 6.0.0, stable 5.4.0

}

group 'org.example'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir("$rootDir/src/main/java")
            srcDir("$buildDir/generated/openapi/src/main/java")
        }
    }
}

dependencies {

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    /* API Generation */
    implementation "org.openapitools:jackson-databind-nullable:0.2.4"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "javax.validation:validation-api"

    implementation 'javax:javaee-api:8.0'

    implementation "io.springfox:springfox-swagger2:2.10.5"
    implementation "org.springdoc:springdoc-openapi-ui:1.6.14"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

tasks.create("openApiGenerate_Api", GenerateTask.class, {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/static/api.yaml".toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = "org.example.profise.api"
    modelPackage = "org.example.profise.dto"
    configOptions = [
            delegatePattern: "true",
            library: "spring-boot",
            dateLibrary : "java8",
            useTags: "true",
            unhandledException: "true",
            additionalModelTypeAnnotations: "@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
            useSwaggerUI: "false",
    ]
    typeMappings = [
            "file" : "org.springframework.web.multipart.MultipartFile"
    ]
})